plugins {
    id 'org.springframework.boot'
    id 'io.spring.dependency-management'
    id 'java'
    id 'maven-publish'
    id 'signing'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier.set("sources")
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier.set("javadoc")
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'graylog-sdk-spring-boot-starter'
            from components.java
            artifact sourcesJar
            artifact javadocJar
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Graylog SDK Spring Boot Starter'
                description = 'Graylog REST API SDK Spring Boot Starter'
                url = 'https://github.com/debugrammer/graylog-sdk-spring-boot'
                licenses {
                    license {
                        name = 'The MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'debugrammer'
                        name = 'Joonsang Park'
                        email = 'debugrammer@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/debugrammer/graylog-sdk-spring-boot.git'
                    developerConnection = 'scm:git:ssh:git@github.com:debugrammer/graylog-sdk-spring-boot.git'
                    url = 'https://github.com/debugrammer/graylog-sdk-spring-boot'
                }
            }
        }
    }
    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"

            if (project.hasProperty('sonatypeUsername') && project.hasProperty('sonatypePassword')) {
                credentials {
                    username sonatypeUsername
                    password sonatypePassword
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-json'
    implementation 'org.springframework.boot:spring-boot-configuration-processor'

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }

    implementation group: 'org.projectlombok', name: 'lombok', version: '1.18.12'
    implementation group: 'com.squareup.okhttp3', name: 'okhttp', version: '4.7.2'
    implementation group: 'com.jayway.jsonpath', name: 'json-path', version: '2.4.0'
    implementation group: 'com.google.guava', name: 'guava', version: '29.0-jre'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.10'

    annotationProcessor("org.projectlombok:lombok:1.18.12")
    testAnnotationProcessor("org.projectlombok:lombok:1.18.12")
}

test {
    useJUnitPlatform()
}

bootJar {
    enabled = false
}

jar {
    enabled = true
    exclude('application.properties', 'sample.application.properties')
}
